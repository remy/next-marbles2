#program nettest
#autoline 10

# Useful reading here https://gitlab.com/thesmog358/tbblue/-/blob/master/docs/extra-hw/wifi/ESPATreadme.TXT

CLEAR 40959: ; use for the espat.sys
RUN AT 3
LAYER 1,2
BORDER 0: PAPER 0: INK 7


LET %n=78: LET %p=0: LET %b=20: LET %t=0
GO SUB 9350

; ON ERROR GO SUB 9710: ; try and wait

PRINT "connecting"


REPEAT
  PROC send()
  PROC onAnyKey()
REPEAT UNTIL 0

DEFPROC onAnyKey()
  REPEAT : REPEAT UNTIL INKEY$ =""
  REPEAT : REPEAT UNTIL INKEY$ <> "" OR ( IN 31=16)
  REPEAT : REPEAT UNTIL INKEY$ =""
ENDPROC

DEFPROC send()
  BANK 21 ERASE 0,512,%$ea
  ; PROC tcpconnect("18.134.205.210",8080)
  PROC tcpconnect("192.168.1.118",8080)
  PROC sendbank(21,0,256)
  PRINT "FIN"
ENDPROC


; %n - bank id
; %o - offset=0
; %l - length=16384
DEFPROC sendbank(%n,%o,%l)
  PRINT "reading ";%l*2;" bytes"
  PRINT #7;"P"+ CHR$ (13)+ CHR$ (10)
  LOCAL %e
  %e=0
  REPEAT
    d=% BANK n DPEEK o
    %o=%o+2
    ; chunk the data into 128 byte blocks (though it's marginally more as it's sent as strings)
;     IF %(o >= l) THEN %e=1
    IF %o&63=0 OR (o >= l) THEN PRINT #7; STR$ (d)+ CHR$ (13)+ CHR$ (10): ELSE : PRINT #7; STR$ (d)+",";
  REPEAT UNTIL %o >= l
  ;PRINT "sending eol"

;   IF %e=0 THEN
  PRINT #7; CHR$ (13)+ CHR$ (10)

  INPUT #7, LINE b$: ; expects to be "ACK"
  ;PRINT b$
  DRIVER 78,3,128 TO %f,%i,%o: ; will clear flags - I'm not entirely sure why I need this
ENDPROC

; standard operations

DEFPROC closeChannels()
  CLOSE # 15: ; connection test line
  CLOSE # 7: ; comms channel
ENDPROC

; See if we can open network if not install drivers
DEFPROC testConnection()
  ON ERROR PROC connect()
  ;PRINT "testing driver connection..."
  OPEN # 15,"D>N": ; "N" is the driver id - the charCode for 78
  ON ERROR
  ;PRINT "driver connected"
ENDPROC

DEFPROC connect()
  ERROR TO e,l,s
  ;PRINT "starting connect logic: e:";e;" l:";l;", s";s
  ON ERROR
  PROC installDrivers()
  DRIVER %n,1,%p: REM 0 to allocate main memory, patch and initialise
  BANK %b ERASE
  ;PRINT "Allocating 1 buffer ";%b
  DRIVER %n,6,%b: ; "Add 16K bank to IPD receive pool"
  DRIVER %n,9,0 TO %p: ;PRINT "115K Baud - prescaler ";%p: ; "Specific UART BAUD rate to be set from lookup table"
  DRIVER %n,3: ;PRINT "IPD Driver started": ; "Get CMD or IPD channel values"
ENDPROC

DEFPROC tcpconnect(n$,p)
  OPEN # 7,"D>N>TCP,"+n$+","+ STR$ (p)
  DRIVER 78,10,1,0: REM set 256/CR buffer mode
ENDPROC

DEFPROC waitTry()
  ERROR TO err,l
  ON ERROR
  IF err <> 15 THEN PRINT "Error ";err;", line: ";l: STOP
  ;PRINT "Retrying tcp connect"
  %t=%t+1
  IF %t > 3 THEN PRINT "Problem connecting to wifi": STOP
ENDPROC

#autoline

# connect

9350 ON ERROR GO TO 9400: ; test connection
9355 PROC closeChannels()
9360 OPEN # 15,"D>N": ; "N" is the driver id - the charCode for 78
9370 RETURN
9400 ERROR TO e,l,s: ; connect
9410 ON ERROR GO TO 9430
9420 .uninstall /nextos/espat.drv
9430 ON ERROR GO TO 9460
9450 .install /nextzxos/espat.drv
9460 LOAD "/nextzxos/espat.sys" CODE 40960: ; in main memory
9470 ON ERROR
9650 DRIVER %n,1,%p: REM 0 to allocate main memory, patch and initialise
9660 BANK %b ERASE
9670 DRIVER %n,6,%b: ; "Add 16K bank to IPD receive pool"
9680 DRIVER %n,9,0 TO %p: ;PRINT "115K Baud - prescaler ";%p: ; "Specific UART BAUD rate to be set from lookup table"
9690 DRIVER %n,3: ;PRINT "IPD Driver started": ; "Get CMD or IPD channel values"
9700 GO TO 9360: ; retry connection

# waitTry()
9710 ERROR TO err,l
9720 ON ERROR
9730 IF err <> 15 THEN PRINT "Error ";err;", line: ";l: STOP
9740 PRINT "Retrying tcp connect"
9750 %t=%t+1
9760 IF %t > 3 THEN PRINT "Problem connecting to wifi": STOP
9770 RETURN
